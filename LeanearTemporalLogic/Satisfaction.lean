import Mathlib
import LeanearTemporalLogic.LTL
import LeanearTemporalLogic.TransitionSystems
import LeanearTemporalLogic.LTProperty

/-!
We will define a satisfaction relation as a type class. This will allow us to define satisfaction for different types of models.
-/
class Satisfaction (Î± : Type u) (Î² : Type v) where
  Satisfies : Î± â†’ Î² â†’ Prop

infixl:70 (priority := high) " âŠ¨ " => Satisfaction.Satisfies

class Equivalent (Î± : Type) where
  Equiv : Î± â†’ Î± â†’ Prop

infixl:70 (priority := high) " â‰¡ " => Equivalent.Equiv

/-!
A world is a sequence of states where each state is set of atomic propositions (that are considered true in that state).
-/
def World (AP: Type) : Type := â„• â†’ Set AP

/-!
A suffix of a world w starting at index i is a world w' such that w'(j) = w(i+j) for all j. We will denote this by w[i...].
-/
def suffix {AP: Type} (Ïƒ : World AP) (i : â„•) : World AP := fun j => Ïƒ (i + j)

syntax:60 term "[" term "â€¦]" : term
macro_rules
  | `($Ïƒ[$iâ€¦]) => `(suffix $Ïƒ $i)

/-!
A simple lemma for composition of suffixes.
-/
theorem suffix_composition {AP: Type} (Ïƒ : World AP) (i j : â„•) : Ïƒ[iâ€¦][jâ€¦] = Ïƒ[i+jâ€¦] := by
  funext k
  unfold suffix
  rw [Nat.add_assoc]

theorem suffix_zero_identity {AP: Type} (Ïƒ : World AP) : Ïƒ[0â€¦] = Ïƒ := by
  funext k
  unfold suffix
  rw [Nat.zero_add]

/-!
Now we define what it means for a world to satisfy an LTL formula.
-/
section
open LTLFormula

def world_satisfies_ltl {AP: Type} (Ïƒ : World AP) : LTLFormula AP â†’ Prop
  | âŠ¤ => true
  | LTLFormula.atom a => a âˆˆ Ïƒ 0
  | Â¬ Ïˆ => Â¬ (world_satisfies_ltl Ïƒ Ïˆ)
  | Ï•â‚ âˆ§ Ï•â‚‚ => (world_satisfies_ltl Ïƒ Ï•â‚) âˆ§ (world_satisfies_ltl Ïƒ Ï•â‚‚)
  | â—¯ Ïˆ => world_satisfies_ltl (Ïƒ[1â€¦]) Ïˆ
  | Ï•â‚ ğ“¤ Ï•â‚‚ => âˆƒ (j: â„•), ((world_satisfies_ltl (Ïƒ[jâ€¦]) Ï•â‚‚) âˆ§ âˆ€ (k: â„•), (k < j â†’ world_satisfies_ltl (Ïƒ[kâ€¦]) Ï•â‚))

instance {AP: Type} : Satisfaction (World AP) (LTLFormula AP) := âŸ¨world_satisfies_ltlâŸ©

/-!
We will also define satisfaction of an LTL formula by a single state, which is the same as satisfaction by a world with that state as the first state and all other states empty.
-/
instance {AP: Type} : Satisfaction (Set AP) (LTLFormula AP) := âŸ¨fun A Ï• => by
  let f : World AP := fun n => if n = 0 then A else âˆ…
  exact f âŠ¨ Ï•âŸ©

/-!
We will also define some useful lemmas for satisfaction.
-/
def world_satisfies_negation {AP: Type} (Ïƒ : World AP) (Ï• : LTLFormula AP) : (Ïƒ âŠ¨ (Â¬ Ï•)) â†” (Â¬ (Ïƒ âŠ¨ Ï•)) := by
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

def world_satisfies_or {AP: Type} (Ïƒ : World AP) (Ï•â‚ Ï•â‚‚ : LTLFormula AP) : (Ïƒ âŠ¨ (Ï•â‚ âˆ¨ Ï•â‚‚)) â†” ((Ïƒ âŠ¨ Ï•â‚) âˆ¨ (Ïƒ âŠ¨ Ï•â‚‚)) := by
  simp [Satisfaction.Satisfies]
  repeat rw [world_satisfies_ltl]
  simp [Or.or, Not.not]
  constructor
  Â· intro h
    contrapose h
    simp at h
    simp
    assumption
  Â· intro h
    contrapose h
    simp at h
    simp
    assumption

def world_satisfies_next {AP: Type} (Ïƒ : World AP) (Ï• : LTLFormula AP) : (Ïƒ âŠ¨ (â—¯ Ï•)) â†” ((Ïƒ[1â€¦]) âŠ¨ Ï•) := by
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

def world_satisfies_and {AP: Type} (Ïƒ : World AP) (Ï•â‚ Ï•â‚‚ : LTLFormula AP) : (Ïƒ âŠ¨ (Ï•â‚ âˆ§ Ï•â‚‚)) â†” ((Ïƒ âŠ¨ Ï•â‚) âˆ§ (Ïƒ âŠ¨ Ï•â‚‚)) := by
  simp [Satisfaction.Satisfies]
  repeat rw [world_satisfies_ltl]

def world_satisfies_until {AP: Type} (Ïƒ : World AP) (Ï•â‚ Ï•â‚‚ : LTLFormula AP) : (Ïƒ âŠ¨ (Ï•â‚ ğ“¤ Ï•â‚‚)) â†” âˆƒ (j: â„•), (((Ïƒ[jâ€¦]) âŠ¨ Ï•â‚‚) âˆ§ âˆ€ (k: â„•), (k < j â†’ ((Ïƒ[kâ€¦]) âŠ¨ Ï•â‚))) := by
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

/-!
We will now show satisfaction for â™¢ and â–¡ operators.
-/
theorem world_satisfies_eventually {AP: Type} (Ïƒ : World AP) (Ï• : LTLFormula AP) : (Ïƒ âŠ¨ (â™¢ Ï•)) â†” âˆƒ (i: â„•), ((Ïƒ[iâ€¦]) âŠ¨ Ï•) := by
  unfold eventually
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

  constructor
  -- left to right
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    obtain âŸ¨hl, hrâŸ© := hi
    use i

  -- right to left
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    use i
    constructor
    Â· assumption
    Â· intro k
      intro hk
      rw [world_satisfies_ltl]

theorem world_satisfies_always {AP: Type} (Ïƒ : World AP) (Ï• : LTLFormula AP) : (Ïƒ âŠ¨ (â–¡ Ï•)) â†” âˆ€ (i: â„•), ((Ïƒ[iâ€¦]) âŠ¨ Ï•) := by
  unfold always

  constructor
  -- left to right
  Â· intro h
    intro i
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl] at h
    have hâ‚ := world_satisfies_eventually Ïƒ (Â¬ Ï•)
    simp [Satisfaction.Satisfies] at hâ‚
    rw [hâ‚] at h
    simp [Not.not] at h
    specialize h i
    rw [world_satisfies_ltl] at h
    simp [Not.not] at h
    simp [Satisfaction.Satisfies]
    assumption

  -- right to left
  Â· intro h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl]
    simp [Not.not]
    have hâ‚ := world_satisfies_eventually Ïƒ (Â¬ Ï•)
    simp [Satisfaction.Satisfies] at hâ‚
    rw [hâ‚]
    simp
    intro i
    rw [world_satisfies_ltl]
    simp [Not.not]
    simp [Satisfaction.Satisfies] at h
    apply h

theorem world_satisfies_always_eventually {AP: Type} (Ïƒ : World AP) (Ï• : LTLFormula AP) : (Ïƒ âŠ¨ (â–¡ â™¢ Ï•)) â†” âˆ€ (i: â„•), âˆƒ (j: â„•), ((Ïƒ[i+jâ€¦]) âŠ¨ Ï•) := by
  constructor

  -- left to right
  Â· intro h
    intro i
    rw [world_satisfies_always] at h
    specialize h i
    rw [world_satisfies_eventually] at h
    obtain âŸ¨j, hjâŸ© := h
    rw [suffix_composition] at hj
    use j

  -- right to left
  Â· intro h
    rw [world_satisfies_always]
    intro i
    rw [world_satisfies_eventually]
    specialize h i
    obtain âŸ¨j, hjâŸ© := h
    use j
    rw [suffix_composition]
    assumption

theorem world_satisfies_eventually_always {AP: Type} (Ïƒ : World AP) (Ï• : LTLFormula AP) : (Ïƒ âŠ¨ (â™¢ â–¡ Ï•)) â†” âˆƒ (i: â„•), âˆ€ (j: â„•), ((Ïƒ[i+jâ€¦]) âŠ¨ Ï•) := by
  constructor

  -- left to right
  Â· intro h
    rw [world_satisfies_eventually] at h
    obtain âŸ¨i, hiâŸ© := h
    use i
    intro j
    rw [world_satisfies_always] at hi
    specialize hi j
    rw [suffix_composition] at hi
    assumption

  -- right to left
  Â· intro h
    rw [world_satisfies_eventually]
    obtain âŸ¨i, hiâŸ© := h
    use i
    rw [world_satisfies_always]
    intro j
    specialize hi j
    rw [suffix_composition]
    assumption


/-!
We now define the set of worlds that satisfy a given LTL formula.
-/
def Worlds {AP: Type} (Ï• : LTLFormula AP) : Set (World AP) := fun Ïƒ => Ïƒ âŠ¨ Ï•

/-!
We will now define the notion of equivalence of LTL formulae.
-/
instance {AP: Type} : Equivalent (LTLFormula AP) := âŸ¨fun Ï• Ïˆ => Worlds Ï• = Worlds ÏˆâŸ©

/-!
It will be useful to show that this is an equivalence relation.
-/
theorem equivalent_ltl_refl {AP: Type} (Ï• : LTLFormula AP) : Ï• â‰¡ Ï• := by
  simp [Equivalent.Equiv]

theorem equivalent_ltl_symm {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (Ï• â‰¡ Ïˆ) â†’ (Ïˆ â‰¡ Ï•) := by
  simp [Equivalent.Equiv]
  intro h
  rw [h]

theorem equivalent_ltl_trans {AP: Type} (Ï• Ïˆ Ï‡ : LTLFormula AP) : (Ï• â‰¡ Ïˆ) â†’ (Ïˆ â‰¡ Ï‡) â†’ (Ï• â‰¡ Ï‡) := by
  simp [Equivalent.Equiv]
  intro hâ‚ hâ‚‚
  rw [hâ‚]
  exact hâ‚‚

/-!
It would also be useful to show that equivalence is preserved by certain operations.
-/
theorem equivalent_ltl_preserves_negation {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (Ï• â‰¡ Ïˆ) â†” ((Â¬ Ï•) â‰¡ (Â¬ Ïˆ)) := by
  simp [Equivalent.Equiv]
  constructor
  Â· intro h
    funext Ïƒ
    have hâ‚ : Worlds Ï• Ïƒ = Worlds Ïˆ Ïƒ := by rw [h]
    simp [Worlds] at hâ‚
    simp [Worlds]
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    simp [Not.not]
    simp [Satisfaction.Satisfies] at hâ‚
    rw [hâ‚]
  Â· intro h
    funext Ïƒ
    have hâ‚ : Worlds (Â¬ Ï•) Ïƒ = Worlds (Â¬ Ïˆ) Ïƒ := by
      simp [Worlds]
      rw [â† Worlds, â† Worlds]
      rw [h]
    simp [Worlds] at hâ‚
    simp [Worlds]
    simp [Satisfaction.Satisfies]
    simp [Satisfaction.Satisfies] at hâ‚
    rw [world_satisfies_ltl, world_satisfies_ltl] at hâ‚
    simp [Not.not] at hâ‚
    constructor
    Â· intro h'
      contrapose h'
      rw [hâ‚]
      assumption
    Â· intro h'
      contrapose h'
      rw [â† hâ‚]
      assumption

theorem equivalent_ltl_preserves_always {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (Ï• â‰¡ Ïˆ) â†’ ((â–¡ Ï•) â‰¡ (â–¡ Ïˆ)) := by
  simp [Equivalent.Equiv]
  intro h
  funext Ïƒ
  unfold Worlds
  rw [world_satisfies_always, world_satisfies_always]
  simp
  constructor
  Â· intro h'
    intro i
    specialize h' i
    have hâ‚ : Worlds Ï• (Ïƒ[iâ€¦]) = Worlds Ïˆ (Ïƒ[iâ€¦]) := by rw [h]
    unfold Worlds at hâ‚
    rw [â† hâ‚]
    assumption
  Â· intro h'
    intro i
    specialize h' i
    have hâ‚ : Worlds Ï• (Ïƒ[iâ€¦]) = Worlds Ïˆ (Ïƒ[iâ€¦]) := by rw [h]
    unfold Worlds at hâ‚
    rw [hâ‚]
    assumption

/-!
Now we prove some equivalence rules for LTL formulae.
-/

theorem ltl_double_negation {AP: Type} (Ï• : LTLFormula AP) : (Â¬ (Â¬ Ï•)) â‰¡ Ï• := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  constructor
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl, world_satisfies_ltl] at h
    simp [Not.not] at h
    simp [Satisfaction.Satisfies]
    assumption
  Â· intro h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    simp [Not.not]
    assumption

theorem ltl_duality_next {AP: Type} (Ï• : LTLFormula AP) : ((Â¬ (â—¯ Ï•)) â‰¡ (â—¯ (Â¬ Ï•))) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  constructor

  -- left to right
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl, world_satisfies_ltl] at h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    assumption

  -- right to left
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl, world_satisfies_ltl] at h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    assumption

theorem ltl_duality_eventually {AP: Type} (Ï• : LTLFormula AP) : ((Â¬ (â™¢ Ï•)) â‰¡ (â–¡ (Â¬ Ï•))) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  constructor

  -- left to right
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl] at h
    have hâ‚ : Â¬ (Ïƒ âŠ¨ (â™¢ Ï•)) := by
      simp [Satisfaction.Satisfies]
      assumption
    rw [world_satisfies_eventually] at hâ‚
    simp [Not.not] at hâ‚
    rw [world_satisfies_always]
    intro i
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl]
    specialize hâ‚ i
    simp [Satisfaction.Satisfies] at hâ‚
    apply hâ‚

  -- right to left
  Â· intro h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl]
    have hâ‚ : Â¬ (Ïƒ âŠ¨ (â™¢ Ï•)) := by
      rw [world_satisfies_eventually]
      simp [Not.not]
      intro i
      simp [Satisfaction.Satisfies]
      rw [world_satisfies_always] at h
      specialize h i
      simp [Satisfaction.Satisfies] at h
      rw [world_satisfies_ltl] at h
      apply h
    simp [Satisfaction.Satisfies] at hâ‚
    apply hâ‚

theorem ltl_duality_always {AP: Type} (Ï• : LTLFormula AP) : ((Â¬ (â–¡ Ï•)) â‰¡ (â™¢ (Â¬ Ï•))) := by
  have hâ‚€ : (Â¬ (â™¢ (Â¬ Ï•))) â‰¡ (â–¡ (Â¬ (Â¬ Ï•))) := ltl_duality_eventually (Â¬ Ï•)
  have hâ‚ : (Â¬ (Â¬ Ï•)) â‰¡ Ï• := ltl_double_negation Ï•
  have hâ‚‚ : (â–¡ (Â¬ (Â¬ Ï•))) â‰¡ (â–¡ Ï•) := equivalent_ltl_preserves_always _ _ hâ‚
  have hâ‚ƒ : (Â¬ â™¢ (Â¬ Ï•)) â‰¡ (â–¡ Ï•) := equivalent_ltl_trans _ _ _ hâ‚€ hâ‚‚
  have hâ‚„ : (Â¬ (Â¬ â™¢ (Â¬ Ï•))) â‰¡ (Â¬ (â–¡ Ï•)) := by
    rw [â† equivalent_ltl_preserves_negation]
    assumption
  have hâ‚… : (Â¬ (Â¬ â™¢ (Â¬ Ï•))) â‰¡ (â™¢ (Â¬ Ï•)) := ltl_double_negation _
  have hâ‚† : (Â¬ (â–¡ Ï•)) â‰¡ (â™¢ (Â¬ Ï•)) := equivalent_ltl_trans _ _ _ hâ‚„ hâ‚…
  assumption

theorem ltl_idempotence_eventually {AP: Type} (Ï• : LTLFormula AP) : (â™¢ (â™¢ Ï•)) â‰¡ (â™¢ Ï•) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  rw [world_satisfies_eventually, world_satisfies_eventually]
  constructor
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    rw [world_satisfies_eventually] at hi
    obtain âŸ¨j, hjâŸ© := hi
    use i + j
    rw [suffix_composition] at hj
    assumption
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    use 0
    rw [world_satisfies_eventually]
    use i
    rw [suffix_composition]
    ring_nf
    assumption

theorem ltl_idempotence_always {AP: Type} (Ï• : LTLFormula AP) : (â–¡ (â–¡ Ï•)) â‰¡ (â–¡ Ï•) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  rw [world_satisfies_always, world_satisfies_always]
  constructor
  Â· intro h
    intro i
    specialize h i
    rw [world_satisfies_always] at h
    specialize h 0
    rw [suffix_composition] at h
    assumption
  Â· intro h
    intro i
    rw [world_satisfies_always]
    intro j
    rw [suffix_composition]
    specialize h (i + j)
    assumption

theorem ltl_idempotence_until_left {AP: Type} (Ï• Ïˆ : LTLFormula AP) : ((Ï• ğ“¤ Ï•) ğ“¤ Ïˆ) â‰¡ (Ï• ğ“¤ Ïˆ) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl, world_satisfies_ltl]
  constructor
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    obtain âŸ¨hj', hj''âŸ© := hj
    use j
    constructor
    Â· assumption
    Â· intro k
      intro hk
      have hk' := hj'' k hk
      rw [world_satisfies_ltl] at hk'
      obtain âŸ¨k', hk''âŸ© := hk'
      obtain âŸ¨hkl, hkrâŸ© := hk''
      rw [suffix_composition] at hkl
      specialize hkr 0
      cases c: k' with
      | zero =>
        rw [c] at hkl
        rw [Nat.add_zero] at hkl
        assumption
      | succ n =>
        have p : 0 < k' := by
          rw [c]
          apply Nat.zero_lt_succ
        specialize hkr p
        rw [suffix_composition] at hkr
        rw [Nat.add_zero] at hkr
        assumption
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    obtain âŸ¨hl, hrâŸ© := hj
    constructor
    Â· assumption
    Â· intro k
      intro hk
      rw [world_satisfies_ltl]
      use 0
      rw [suffix_composition, Nat.add_zero]
      specialize hr k hk
      constructor
      Â· assumption
      Â· intro k'
        intro hk'
        simp at hk'

theorem ltl_idempotence_until_right {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (Ï• ğ“¤ (Ïˆ ğ“¤ Ïˆ)) â‰¡ (Ï• ğ“¤ Ïˆ) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl, world_satisfies_ltl]
  constructor
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    obtain âŸ¨hl, hrâŸ© := hj
    rw [world_satisfies_ltl] at hl
    obtain âŸ¨j', hj'âŸ© := hl
    obtain âŸ¨hjl, hjrâŸ© := hj'
    rw [suffix_composition] at hjl
    specialize hjr 0
    cases c: j' with
    | zero =>
      rw [c] at hjl
      rw [Nat.add_zero] at hjl
      constructor
      Â· assumption
      Â· assumption
    | succ n =>
      have p : 0 < j' := by
        rw [c]
        apply Nat.zero_lt_succ
      specialize hjr p
      rw [suffix_composition] at hjr
      rw [Nat.add_zero] at hjr
      constructor
      Â· assumption
      Â· assumption

  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    obtain âŸ¨hl, hrâŸ© := hj
    constructor
    Â· rw [world_satisfies_ltl]
      use 0
      rw [suffix_composition, Nat.add_zero]
      constructor
      Â· assumption
      Â· intro k
        intro hk
        simp at hk
    Â· assumption

theorem ltl_absorption_always_eventually {AP: Type} (Ï• : LTLFormula AP) : (â™¢ â–¡ â™¢ Ï•) â‰¡ (â–¡ â™¢ Ï•) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  rw [world_satisfies_eventually]
  constructor
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    rw [world_satisfies_always_eventually] at hi
    rw [world_satisfies_always_eventually]
    intro i'
    specialize hi i'
    obtain âŸ¨j, hjâŸ© := hi
    use i + j
    rw [suffix_composition] at hj
    rw [â† Nat.add_assoc, Nat.add_comm i' i]
    rw [â† Nat.add_assoc] at hj
    assumption
  Â· intro h
    use 0
    rw [suffix_zero_identity]
    assumption

theorem ltl_absorption_eventually_always {AP: Type} (Ï• : LTLFormula AP) : (â–¡ â™¢ â–¡ Ï•) â‰¡ (â™¢ â–¡ Ï•) := by
  simp [Equivalent.Equiv]
  funext Ïƒ
  simp [Worlds]
  rw [world_satisfies_always]
  constructor
  Â· intro h
    specialize h 0
    rw [suffix_zero_identity] at h
    assumption
  Â· intro h
    intro i
    rw [world_satisfies_eventually_always] at h
    obtain âŸ¨i', hiâŸ© := h
    rw [world_satisfies_eventually_always]
    use i'
    intro j
    specialize hi (i + j)
    rw [suffix_composition]
    rw [â† Nat.add_assoc]
    rw [â† Nat.add_assoc, Nat.add_comm i' i] at hi
    assumption

theorem ltl_expansion_until {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (Ï• ğ“¤ Ïˆ) â‰¡ (Ïˆ âˆ¨ (Ï• âˆ§ (â—¯ (Ï• ğ“¤ Ïˆ)))) := by
  simp only [Equivalent.Equiv]
  funext Ïƒ
  simp only [Worlds]
  rw [world_satisfies_or]
  simp [Satisfaction.Satisfies]
  constructor
  Â· intro h
    rw [world_satisfies_ltl] at h
    obtain âŸ¨j, hjâŸ© := h
    obtain âŸ¨hl, hrâŸ© := hj
    rw [world_satisfies_ltl]
    cases c: j with
    | zero =>
      rw [c] at hl
      rw [suffix_zero_identity] at hl
      left
      assumption
    | succ n =>
      right
      have p : 0 < j := by
        rw [c]
        apply Nat.zero_lt_succ
      have hr' := hr 0 p
      rw [suffix_zero_identity] at hr'
      constructor
      Â· assumption
      Â· repeat rw [world_satisfies_ltl]
        use n
        rw [suffix_composition]
        rw [c] at hl
        rw [Nat.add_comm]
        constructor
        Â· assumption
        Â· intro k
          intro hk
          rw [suffix_composition]
          have p' : k + 1 < j := by
            rw [c]
            have p'' : k + 1 < n + 1 := by
              apply Nat.succ_lt_succ
              assumption
            assumption
          specialize hr (k + 1) p'
          rw [Nat.add_comm]
          assumption
  Â· intro h
    rw [world_satisfies_ltl]
    cases h with
    | inl hl =>
        use 0
        rw [suffix_zero_identity]
        constructor
        Â· assumption
        Â· simp
    | inr hr =>
        rw [world_satisfies_ltl] at hr
        obtain âŸ¨hll, hrrâŸ© := hr
        repeat rw [world_satisfies_ltl] at hrr
        obtain âŸ¨j, hjâŸ© := hrr
        use j + 1
        rw [suffix_composition, Nat.add_comm] at hj
        obtain âŸ¨hjl, hjrâŸ© := hj
        constructor
        Â· assumption
        Â· intro k
          intro hk
          cases c: k with
          | zero =>
            rw [suffix_zero_identity]
            assumption
          | succ n =>
            rw [c] at hk
            rw [Nat.succ_lt_succ_iff] at hk
            specialize hjr n hk
            rw [suffix_composition] at hjr
            rw [Nat.add_comm]
            assumption

theorem ltl_expansion_eventually {AP: Type} (Ï• : LTLFormula AP) : (â™¢ Ï•) â‰¡ (Ï• âˆ¨ (â—¯ (â™¢ Ï•))) := by
  simp only [Equivalent.Equiv]
  funext Ïƒ
  simp only [Worlds]
  rw [world_satisfies_or]
  simp
  constructor
  Â· intro h
    rw [world_satisfies_eventually] at h
    obtain âŸ¨i, hiâŸ© := h
    cases c: i with
    | zero =>
      rw [c] at hi
      rw [suffix_zero_identity] at hi
      left
      assumption
    | succ n =>
      right
      rw [world_satisfies_next]
      rw [world_satisfies_eventually]
      use n
      rw [suffix_composition]
      rw [c] at hi
      rw [Nat.add_comm]
      assumption
  Â· intro h
    rw [world_satisfies_eventually]
    cases h with
    | inl hl =>
      use 0
      rw [suffix_zero_identity]
      assumption
    | inr hr =>
      rw [world_satisfies_next] at hr
      rw [world_satisfies_eventually] at hr
      obtain âŸ¨j, hjâŸ© := hr
      use j + 1
      rw [suffix_composition, Nat.add_comm] at hj
      assumption

theorem ltl_expansion_always {AP: Type} (Ï• : LTLFormula AP) : (â–¡ Ï•) â‰¡ (Ï• âˆ§ (â—¯ (â–¡ Ï•))) := by
  simp only [Equivalent.Equiv]
  funext Ïƒ
  simp only [Worlds]
  rw [world_satisfies_and]
  simp
  rw [world_satisfies_next]
  repeat rw [world_satisfies_always]
  constructor
  Â· intro h
    constructor
    Â· specialize h 0
      rw [suffix_zero_identity] at h
      assumption
    Â· intro i
      specialize h (i + 1)
      rw [suffix_composition, Nat.add_comm]
      assumption
  Â· intro h
    intro i
    obtain âŸ¨hl, hrâŸ© := h
    cases c: i with
    | zero =>
      rw [suffix_zero_identity]
      assumption
    | succ n =>
      specialize hr n
      rw [suffix_composition, Nat.add_comm] at hr
      assumption

theorem ltl_distributive_next_until {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (â—¯ (Ï• ğ“¤ Ïˆ)) â‰¡ ((â—¯ Ï•) ğ“¤ (â—¯ Ïˆ)) := by
  simp only [Equivalent.Equiv]
  funext Ïƒ
  simp only [Worlds]
  rw [world_satisfies_next]
  repeat rw [world_satisfies_until]
  simp
  constructor
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    rw [suffix_composition] at hj
    rw [world_satisfies_next]
    rw [suffix_composition]
    rw [Nat.add_comm]
    obtain âŸ¨hl, hrâŸ© := hj
    constructor
    Â· assumption
    Â· intro k
      intro hk
      specialize hr k hk
      rw [world_satisfies_next]
      rw [suffix_composition]
      rw [suffix_composition] at hr
      rw [Nat.add_comm]
      assumption
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    rw [world_satisfies_next] at hj
    rw [suffix_composition] at hj
    rw [suffix_composition]
    rw [Nat.add_comm]
    obtain âŸ¨hl, hrâŸ© := hj
    constructor
    Â· assumption
    Â· intro k
      intro hk
      specialize hr k hk
      rw [world_satisfies_next] at hr
      rw [suffix_composition]
      rw [suffix_composition] at hr
      rw [Nat.add_comm]
      assumption

theorem ltl_distributive_eventually_or {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (â™¢ (Ï• âˆ¨ Ïˆ)) â‰¡ ((â™¢ Ï•) âˆ¨ (â™¢ Ïˆ)) := by
  simp only [Equivalent.Equiv]
  funext Ïƒ
  simp only [Worlds]
  rw [world_satisfies_eventually]
  repeat rw [world_satisfies_or]
  simp only [eq_iff_iff]
  constructor
  Â· intro h
    repeat rw [world_satisfies_eventually]
    obtain âŸ¨i, hiâŸ© := h
    rw [world_satisfies_or] at hi
    cases hi with
    | inl hl =>
      left
      use i
    | inr hr =>
      right
      use i
  Â· intro h
    cases h with
    | inl hl =>
      rw [world_satisfies_eventually] at hl
      obtain âŸ¨i, hiâŸ© := hl
      use i
      rw [world_satisfies_or]
      left
      assumption
    | inr hr =>
      rw [world_satisfies_eventually] at hr
      obtain âŸ¨i, hiâŸ© := hr
      use i
      rw [world_satisfies_or]
      right
      assumption

theorem ltl_distributive_always_and {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (â–¡ (Ï• âˆ§ Ïˆ)) â‰¡ ((â–¡ Ï•) âˆ§ (â–¡ Ïˆ)) := by
  simp only [Equivalent.Equiv]
  funext Ïƒ
  simp only [Worlds]
  rw [world_satisfies_always]
  repeat rw [world_satisfies_and]
  simp only [eq_iff_iff]
  constructor
  Â· intro h
    repeat rw [world_satisfies_always]
    constructor
    Â· intro i
      specialize h i
      rw [world_satisfies_and] at h
      obtain âŸ¨hl, hrâŸ© := h
      assumption
    Â· intro i
      specialize h i
      rw [world_satisfies_and] at h
      obtain âŸ¨hl, hrâŸ© := h
      assumption
  Â· intro h
    repeat rw [world_satisfies_always] at h
    intro i
    obtain âŸ¨hl, hrâŸ© := h
    specialize hl i
    specialize hr i
    rw [world_satisfies_and]
    constructor
    Â· assumption
    Â· assumption


/-!
Now we prove the lemma that "Until is the Least Solution of the Expansion Law"
-/
def solution_of_expansion_law {AP: Type} (Ï• Ïˆ : LTLFormula AP) (P : Set (World AP)) : Prop := (Worlds Ïˆ âˆª {Ïƒ âˆˆ Worlds Ï• | Ïƒ[1â€¦] âˆˆ P}) âŠ† P

theorem until_least_solution_of_expansion_law {AP: Type} (Ï• Ïˆ : LTLFormula AP) : (solution_of_expansion_law Ï• Ïˆ (Worlds (Ï• ğ“¤ Ïˆ))) âˆ§ (âˆ€ P, (solution_of_expansion_law Ï• Ïˆ P) â†’ Worlds (Ï• ğ“¤ Ïˆ) âŠ† P) := by
  unfold solution_of_expansion_law
  unfold Worlds
  simp
  constructor

  -- we first show that it is indeed a solution
  Â· constructor
    Â· intro Ïƒ
      intro h
      rw [Set.mem_def] at h
      rw [Set.mem_def]
      rw [world_satisfies_until]
      use 0
      rw [suffix_zero_identity]
      constructor
      Â· assumption
      Â· intro k
        intro hk
        simp at hk
    Â· intro Ïƒ
      intro h
      rw [Set.mem_sep_iff] at h
      rw [Set.mem_def] at h
      rw [Set.mem_def]
      obtain âŸ¨hl, hrâŸ© := h
      rw [Set.mem_def] at hr
      rw [world_satisfies_until]
      rw [world_satisfies_until] at hr
      obtain âŸ¨j, hjâŸ© := hr
      rw [suffix_composition] at hj
      use (1 + j)
      obtain âŸ¨hjl, hjrâŸ© := hj
      constructor
      Â· assumption
      Â· intro k
        intro hk
        cases c: k with
        | zero =>
          rw [suffix_zero_identity]
          assumption
        | succ n =>
          rw [c] at hk
          rw [Nat.add_comm 1 j] at hk
          rw [Nat.succ_lt_succ_iff] at hk
          specialize hjr n hk
          rw [suffix_composition] at hjr
          rw [Nat.add_comm]
          assumption

  -- now we show that it is the least solution
  Â· intro P
    intro h
    intro hâ‚
    rw [Set.subset_def]
    intro Ïƒ
    intro h'
    rw [Set.mem_def] at h'
    rw [world_satisfies_until] at h'
    obtain âŸ¨j, hjâŸ© := h'
    obtain âŸ¨hjl, hjrâŸ© := hj
    rw [Set.subset_def] at h
    specialize h (Ïƒ[jâ€¦])
    rw [Set.mem_def] at h
    apply h at hjl

    -- we perform backwards induction on j
    let b (k: â„•) : âˆ€ (n: â„•), (j = n + k) â†’ (Ïƒ[nâ€¦]) âˆˆ P := by
      induction k with
      | zero =>
        intro n
        intro hn
        simp at hn
        rw [â† hn]
        assumption
      | succ m ih =>
        intro n
        intro hn
        rw [Nat.add_comm m 1] at hn
        rw [â† Nat.add_assoc] at hn
        specialize ih (n + 1) hn
        have hâ‚€ : 0 < (1 + m) := by
          apply Nat.zero_lt_of_ne_zero
          rw [Nat.add_comm]
          apply Nat.succ_ne_zero
        rw [â† @Nat.add_lt_add_iff_left n, Nat.add_zero, â† Nat.add_assoc, â† hn] at hâ‚€
        specialize hjr n hâ‚€
        rw [Set.subset_def] at hâ‚
        specialize hâ‚ (Ïƒ[nâ€¦])
        apply hâ‚
        rw [Set.mem_sep_iff]
        rw [Set.mem_def]
        constructor
        Â· assumption
        Â· rw [suffix_composition]
          assumption

    have hâ‚€ : Ïƒ[0â€¦] âˆˆ P := by
      apply b j 0
      simp

    rw [suffix_zero_identity] at hâ‚€
    assumption

/-!
We will now use the satisfaction of LTL formulae to define satisfaction of PL formulae.
-/
instance {AP: Type} : Satisfaction (Set AP) (PLFormula AP) := âŸ¨fun A Î¦ â†¦ A âŠ¨ Î¦.formulaâŸ©

/-!
We will also define some useful lemmas for satisfaction of PL formulae.
-/
def set_satisfies_negation {AP: Type} (Ïƒ : Set AP) (Ï• : PLFormula AP) : (Ïƒ âŠ¨ (Â¬ Ï•)) â†” (Â¬ (Ïƒ âŠ¨ Ï•)) := by
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

def set_satisfies_or {AP: Type} (Ïƒ : Set AP) (Ï•â‚ Ï•â‚‚ : PLFormula AP) : (Ïƒ âŠ¨ (Ï•â‚ âˆ¨ Ï•â‚‚)) â†” ((Ïƒ âŠ¨ Ï•â‚) âˆ¨ (Ïƒ âŠ¨ Ï•â‚‚)) := by
  simp [Satisfaction.Satisfies]
  repeat rw [world_satisfies_ltl]
  simp [Or.or, Not.not]
  constructor
  Â· intro h
    contrapose h
    simp at h
    simp
    assumption
  Â· intro h
    contrapose h
    simp at h
    simp
    assumption

def set_satisfies_and {AP: Type} (Ïƒ : Set AP) (Ï•â‚ Ï•â‚‚ : PLFormula AP) : (Ïƒ âŠ¨ (Ï•â‚ âˆ§ Ï•â‚‚)) â†” ((Ïƒ âŠ¨ Ï•â‚) âˆ§ (Ïƒ âŠ¨ Ï•â‚‚)) := by
  simp [Satisfaction.Satisfies]
  repeat rw [world_satisfies_ltl]

end section

section
open TransitionSystem
/-!
We will define a satisfaction relation between transition systems and LT properties. For this, both must be defined over the same set of atomic propositions. Note that we specifically deal with Transition Systems without terminal states.
-/
instance {AP: Type} : Satisfaction (TransitionSystemWTS AP) (LTProperty AP) := âŸ¨fun TSwts P â†¦ TracesWTS TSwts âŠ† PâŸ©

instance {AP: Type} {TSwts: TransitionSystemWTS AP} : Satisfaction (TSwts.TS.S) (LTProperty AP) := âŸ¨fun s P â†¦ TracesFromStateWTS s âŠ† PâŸ©

-- We need to define membership of an Infinite Trace in an LT Property
instance {AP: Type} : Membership (InfiniteTrace AP) (LTProperty AP) := âŸ¨fun P Ï€ â†¦ by
  rw [LTProperty] at P
  rw [InfiniteTrace] at Ï€
  exact Ï€ âˆˆ PâŸ©

/-!
Some auxiliary lemmas about satisfaction of LT properties.
-/
theorem ltproperty_satisfaction_allPaths {AP: Type} (TSwts: TransitionSystemWTS AP) (P: LTProperty AP) : TSwts âŠ¨ P â†” âˆ€ Ï€, (h: Ï€ âˆˆ Paths TSwts.TS) â†’ TraceFromPathWTS Ï€ h âˆˆ P := by
  simp [Satisfaction.Satisfies]
  rw [TracesWTS]
  simp
  constructor
  Â· intro h
    intro Ï€
    intro h'
    rw [Paths] at h'
    simp at h'
    rw [isPath] at h'
    obtain âŸ¨hinit, hmaxâŸ© := h'
    rw [isInitialPathFragment] at hinit
    specialize h (startStatePathFragment Ï€)
    apply h at hinit
    rw [TracesFromInitialStateWTS] at hinit
    rw [Set.setOf_subset] at hinit
    specialize hinit (TraceFromPathWTS Ï€ h')
    apply hinit
    use Ï€
    use path_starts_from_startState Ï€ h'
    unfold TraceFromPathFromInitialStateWTS
    simp
  Â· intro h
    intro s
    intro h'
    unfold TracesFromInitialStateWTS
    rw [Set.setOf_subset]
    intro trace
    intro h''
    obtain âŸ¨Ï€, hÏ€âŸ© := h''
    obtain âŸ¨hÏ€', hÏ€''âŸ© := hÏ€
    specialize h Ï€
    have hâ‚€: Ï€ âˆˆ Paths TSwts.TS := by
      rw [Paths]
      rw [PathsFromState] at hÏ€'
      simp at hÏ€'
      simp
      unfold isPath
      obtain âŸ¨hl, hrâŸ© := hÏ€'
      constructor
      Â· unfold isInitialPathFragment
        rw [hr]
        assumption
      Â· assumption
    apply h at hâ‚€
    rw [TraceFromPathFromInitialStateWTS] at hÏ€''
    rw [â† hÏ€''] at hâ‚€
    assumption


/-!
We now prove a theorem about **Trace Inclusion and LT Properties**.
-/
theorem trace_inclusion_and_LTProperties {AP: Type} (TSwtsâ‚ TSwtsâ‚‚: TransitionSystemWTS AP) : (TracesWTS TSwtsâ‚ âŠ† TracesWTS TSwtsâ‚‚) â†” âˆ€ (P: LTProperty AP), TSwtsâ‚‚ âŠ¨ P â†’ TSwtsâ‚ âŠ¨ P := by
  simp [Satisfaction.Satisfies]
  constructor
  Â· intro h
    intro P
    intro h'
    rw [Set.subset_def]
    rw [Set.subset_def] at h
    rw [Set.subset_def] at h'
    intro Ïƒ
    intro h''
    specialize h Ïƒ
    apply h at h''
    specialize h' Ïƒ h''
    assumption
  Â· intro h
    specialize h (TracesWTS TSwtsâ‚‚)
    simp at h
    assumption


/-!
We will define the notion of trace equivalence between two transition systems, and then show a corollary of the previous theorem about **Trace Equivalence and LT Properties**.
-/
def trace_equivalence {AP: Type} (TSâ‚ TSâ‚‚: TransitionSystem AP) : Prop := Traces TSâ‚ = Traces TSâ‚‚

def trace_equivalence_wts {AP: Type} (TSwtsâ‚ TSwtsâ‚‚: TransitionSystemWTS AP) : Prop := TracesWTS TSwtsâ‚ = TracesWTS TSwtsâ‚‚

theorem trace_equivalence_and_LTProperties {AP: Type} (TSwtsâ‚ TSwtsâ‚‚: TransitionSystemWTS AP) : (trace_equivalence_wts TSwtsâ‚ TSwtsâ‚‚) â†” âˆ€ (P: LTProperty AP), TSwtsâ‚ âŠ¨ P â†” TSwtsâ‚‚ âŠ¨ P := by
  rw [trace_equivalence_wts]
  constructor
  Â· intro h
    have hâ‚€ : TracesWTS TSwtsâ‚ âŠ† TracesWTS TSwtsâ‚‚ := by rw [h]
    rw [trace_inclusion_and_LTProperties] at hâ‚€
    have hâ‚ : TracesWTS TSwtsâ‚‚ âŠ† TracesWTS TSwtsâ‚ := by rw [h]
    rw [trace_inclusion_and_LTProperties] at hâ‚
    intro P
    constructor
    Â· apply hâ‚
    Â· apply hâ‚€
  Â· intro h
    rw [Set.Subset.antisymm_iff]
    rw [trace_inclusion_and_LTProperties, trace_inclusion_and_LTProperties]
    constructor
    Â· intro P
      specialize h P
      rw [iff_def'] at h
      obtain âŸ¨hâ‚, hâ‚‚âŸ© := h
      apply hâ‚
    Â· intro P
      specialize h P
      rw [iff_def'] at h
      obtain âŸ¨hâ‚, hâ‚‚âŸ© := h
      apply hâ‚‚

/-!
We will now define some special kinds of LT properties, starting with **Invariants**.
-/
def isInvariantWithFormula {AP: Type} (P: LTProperty AP) (Ï•: PLFormula AP) : Prop := P = {Ïƒ | âˆ€ (n: â„•), Ïƒ n âŠ¨ Ï•}
def isInvariant {AP: Type} (P: LTProperty AP) : Prop := âˆƒ (Ï• : PLFormula AP), isInvariantWithFormula P Ï•


theorem invariant_satisfaction_reachability {AP: Type} (TSwts: TransitionSystemWTS AP) (P: LTProperty AP) (h: isInvariant P) : TSwts âŠ¨ P â†” (âˆƒ (Ï• : PLFormula AP), (isInvariantWithFormula P Ï•) âˆ§ (âˆ€ s âˆˆ Reach TSwts.TS, TSwts.TS.L s âŠ¨ Ï•)) := by
  rw [ltproperty_satisfaction_allPaths]
  rw [isInvariant] at h
  obtain âŸ¨Ï•, hÏ•âŸ© := h
  unfold isInvariantWithFormula at hÏ•
  obtain âŸ¨TS, hTSâŸ© := TSwts
  let hTS' := hTS
  rw [hasNoTerminalStates] at hTS
  constructor
  Â· intro h'
    use Ï•
    constructor
    Â· assumption
    Â· intro s
      intro hs
      rw [Reach] at hs
      simp at hs
      unfold isReachableState at hs
      obtain âŸ¨n, e, heâŸ© := hs
      obtain âŸ¨hel, herâŸ© := he
      let Ï€tail : FinitePathFragment TS n := finiteExecutionFragmentToFinitePathFragment e
      have htail : Ï€tail.states = e.states := by
        unfold Ï€tail finiteExecutionFragmentToFinitePathFragment
        simp
      obtain âŸ¨tailStates, tailValidâŸ© := Ï€tail
      simp at htail
      have hhead : âˆƒ Ï€', Ï€' âˆˆ PathsFromState s := path_originates_from_state_if_noTerminalState hTS s
      obtain âŸ¨Ï€head, hÏ€headâŸ© := hhead
      simp
      simp at Ï€head
      simp at h'
      simp at s
      cases c: Ï€head with
      | @finite n p =>
        rw [c] at hÏ€head
        unfold PathsFromState at hÏ€head
        simp at hÏ€head
        obtain âŸ¨hÏ€headmax, _âŸ© := hÏ€head
        unfold isMaximalPathFragment endStatePathFragment at hÏ€headmax
        simp at hÏ€headmax
        specialize hTS (p.states (Fin.last n))
        contradiction
      | infinite p =>
        rw [c] at hÏ€head
        obtain âŸ¨headStates, headValidâŸ© := p

        unfold PathsFromState startStatePathFragment at hÏ€head
        simp at hÏ€head
        obtain âŸ¨_, headState0âŸ© := hÏ€head

        -- combine Ï€tail and Ï€head to form a path
        let Ï€ := PathFragment.infinite (âŸ¨fun i => if i < n then tailStates i else headStates (i - n),
          by
            intro i
            simp
            if h : i < n then
              let v := tailValid (Fin.mk i h)
              simp at v
              simp [h]
              if h': i + 1 < n then
                simp [h']
                exact v
              else
                simp at h'
                rw [Nat.lt_iff_add_one_le] at h
                have p : i + 1 = n := by apply Nat.le_antisymm <;> assumption
                rw [p]
                simp
                rw [headState0]
                if hn: n = 0 then
                  rw [hn] at p
                  simp at p
                else
                  have hnsucc : âˆƒ m, n = m + 1 := by
                    apply Nat.exists_eq_succ_of_ne_zero
                    assumption
                  obtain âŸ¨m, hmâŸ© := hnsucc
                  rw [hm] at p
                  simp at p
                  rw [p]
                  let v := tailValid (Fin.mk m (by rw [hm]; simp))
                  simp at v
                  have hv : tailStates (m + 1) = s := by
                    rw [htail]
                    unfold endStateExecutionFragment at her
                    rw [â† Fin.natCast_eq_last] at her
                    rw [â† her]
                    simp [hm]
                  rw [â† hv]
                  exact v
            else
              simp [h]
              simp at h
              have h' : Â¬ (i + 1 < n) := by
                simp only [LTLFormula.Not.not]
                contrapose h
                simp at h
                rw [Nat.not_le]
                have hii : i < i + 1 := by simp
                apply Nat.lt_trans hii h
              simp only [LTLFormula.Not.not] at h'
              simp [h']
              let v : headStates (i - n + 1) âˆˆ Post (headStates (i - n)) := headValid (i - n)
              rw [Nat.sub_add_comm h]
              exact v
            âŸ©: InfinitePathFragment TS)

        have hÏ€ : Ï€ âˆˆ Paths TS := by
          unfold Paths isPath isInitialPathFragment isMaximalPathFragment endStatePathFragment
          simp
          constructor
          Â· unfold startStatePathFragment Ï€
            simp
            unfold isInitialExecutionFragment startStateExecutionFragment at hel
            simp at hel
            cases n with
            | zero =>
              simp
              rw [headState0]
              unfold endStateExecutionFragment at her
              rw [â† Fin.natCast_eq_last] at her
              rw [â† her]
              simp [hel]
            | succ m =>
              simp
              rw [htail]
              assumption
          Â· unfold Ï€
            simp

        specialize h' Ï€ hÏ€
        rw [hÏ•] at h'
        rw [Set.mem_def] at h'
        rw [Set.setOf_app_iff] at h'
        specialize h' n

        have hs : (@TraceFromPathWTS AP âŸ¨TS, hTSâŸ© Ï€ hÏ€) n = TS.L s := by
          unfold Ï€ TraceFromPathWTS InfiniteTraceFromInfinitePathFragment
          simp only [TraceFromPathWTS.proof_2]
          sorry

        rw [hs] at h'
        assumption
  Â· intro h
    intro Ï€
    intro hÏ€
    simp at hÏ€
    rw [hÏ•, Set.mem_def]
    rw [Set.setOf_app_iff]
    intro n
    obtain âŸ¨Î¦, hÎ¦âŸ© := h
    obtain âŸ¨hÎ¦l, hÎ¦râŸ© := hÎ¦
    sorry

end section
