import Mathlib
import LeanearTemporalLogic.LTL

/-!
We will define a satisfaction relation as a type class. This will allow us to define satisfaction for different types of models.
-/
class Satisfaction (Î± Î² : Type) where
  Satisfies : Î± â†’ Î² â†’ Prop

infixl:70 (priority := high) " âŠ¨ " => Satisfaction.Satisfies

class Equivalent (Î± : Type) where
  Equiv : Î± â†’ Î± â†’ Prop

infixl:70 (priority := high) " â‰¡ " => Equivalent.Equiv

/-!
A world is a sequence of states where each state is set of atomic propositions (that are considered true in that state).
-/
def World : Type := â„• â†’ Set AP

/-!
A suffix of a world w starting at index i is a world w' such that w'(j) = w(i+j) for all j. We will denote this by w[i...].
-/
def suffix (Ïƒ : World) (i : â„•) : World := fun j => Ïƒ (i + j)

syntax:60 term "[" term "â€¦]" : term
macro_rules
  | `($Ïƒ[$iâ€¦]) => `(suffix $Ïƒ $i)

/-!
A simple lemma for composition of suffixes.
-/
def suffix_composition (Ïƒ : World) (i j : â„•) : Ïƒ[iâ€¦][jâ€¦] = Ïƒ[i+jâ€¦] := by
  funext k
  unfold suffix
  rw [Nat.add_assoc]

/-!
Now we define what it means for a world to satisfy an LTL formula.
-/
section
open LTLFormula

def world_satisfies_ltl (Ïƒ : World) : LTLFormula â†’ Prop
  | âŠ¤ => true
  | LTLFormula.atom a => a âˆˆ Ïƒ 0
  | Â¬ Ïˆ => Â¬ (world_satisfies_ltl Ïƒ Ïˆ)
  | Ï•â‚ âˆ§ Ï•â‚‚ => (world_satisfies_ltl Ïƒ Ï•â‚) âˆ§ (world_satisfies_ltl Ïƒ Ï•â‚‚)
  | â—¯ Ïˆ => world_satisfies_ltl (Ïƒ[1â€¦]) Ïˆ
  | Ï•â‚ ð“¤ Ï•â‚‚ => âˆƒ (j: â„•), ((world_satisfies_ltl (Ïƒ[jâ€¦]) Ï•â‚‚) âˆ§ âˆ€ (k: â„•), (k < j â†’ world_satisfies_ltl (Ïƒ[kâ€¦]) Ï•â‚))

instance : Satisfaction World LTLFormula := âŸ¨world_satisfies_ltlâŸ©

/-!
We will also define some useful lemmas for satisfaction.
-/
def world_satisfies_negation (Ïƒ : World) (Ï• : LTLFormula) : (Ïƒ âŠ¨ (Â¬ Ï•)) â†” (Â¬ (Ïƒ âŠ¨ Ï•)) := by
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

/-!
We will now show satisfaction for â™¢ and â–¡ operators.
-/
theorem world_satisfies_eventually (Ïƒ : World) (Ï• : LTLFormula) : (Ïƒ âŠ¨ (â™¢ Ï•)) â†” âˆƒ (i: â„•), ((Ïƒ[iâ€¦]) âŠ¨ Ï•) := by
  unfold eventually
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl]

  constructor
  -- left to right
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    obtain âŸ¨hl, hrâŸ© := hi
    use i

  -- right to left
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    use i
    constructor
    Â· assumption
    Â· intro k
      intro hk
      rw [world_satisfies_ltl]

theorem world_satisfies_always (Ïƒ : World) (Ï• : LTLFormula) : (Ïƒ âŠ¨ (â–¡ Ï•)) â†” âˆ€ (i: â„•), ((Ïƒ[iâ€¦]) âŠ¨ Ï•) := by
  unfold always

  constructor
  -- left to right
  Â· intro h
    intro i
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl] at h
    have hâ‚ := world_satisfies_eventually Ïƒ (Â¬ Ï•)
    simp [Satisfaction.Satisfies] at hâ‚
    rw [hâ‚] at h
    simp [Not.not] at h
    specialize h i
    rw [world_satisfies_ltl] at h
    simp [Not.not] at h
    simp [Satisfaction.Satisfies]
    assumption

  -- right to left
  Â· intro h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl]
    simp [Not.not]
    have hâ‚ := world_satisfies_eventually Ïƒ (Â¬ Ï•)
    simp [Satisfaction.Satisfies] at hâ‚
    rw [hâ‚]
    simp
    intro i
    rw [world_satisfies_ltl]
    simp [Not.not]
    simp [Satisfaction.Satisfies] at h
    apply h

theorem world_satisfies_always_eventually (Ïƒ : World) (Ï• : LTLFormula) : (Ïƒ âŠ¨ (â–¡ â™¢ Ï•)) â†” âˆ€ (i: â„•), âˆƒ (j: â„•), ((Ïƒ[i+jâ€¦]) âŠ¨ Ï•) := by
  constructor

  -- left to right
  Â· intro h
    intro i
    rw [world_satisfies_always] at h
    specialize h i
    rw [world_satisfies_eventually] at h
    obtain âŸ¨j, hjâŸ© := h
    rw [suffix_composition] at hj
    use j

  -- right to left
  Â· intro h
    rw [world_satisfies_always]
    intro i
    rw [world_satisfies_eventually]
    specialize h i
    obtain âŸ¨j, hjâŸ© := h
    use j
    rw [suffix_composition]
    assumption

theorem world_satisfies_eventually_always (Ïƒ : World) (Ï• : LTLFormula) : (Ïƒ âŠ¨ (â™¢ â–¡ Ï•)) â†” âˆƒ (i: â„•), âˆ€ (j: â„•), ((Ïƒ[i+jâ€¦]) âŠ¨ Ï•) := by
  constructor

  -- left to right
  Â· intro h
    rw [world_satisfies_eventually] at h
    obtain âŸ¨i, hiâŸ© := h
    use i
    intro j
    rw [world_satisfies_always] at hi
    specialize hi j
    rw [suffix_composition] at hi
    assumption

  -- right to left
  Â· intro h
    rw [world_satisfies_eventually]
    obtain âŸ¨i, hiâŸ© := h
    use i
    rw [world_satisfies_always]
    intro j
    specialize hi j
    rw [suffix_composition]
    assumption


/-!
We now define the set of worlds that satisfy a given LTL formula.
-/
def Worlds (Ï• : LTLFormula) : Set World := fun Ïƒ => Ïƒ âŠ¨ Ï•

/-!
We will now define the notion of equivalence of LTL formulae.
-/
def equivalent_ltl (Ï• Ïˆ : LTLFormula) : Prop := Worlds Ï• = Worlds Ïˆ

instance : Equivalent LTLFormula := âŸ¨equivalent_ltlâŸ©

/-!
It will be useful to show that this is an equivalence relation.
-/
theorem equivalent_ltl_refl (Ï• : LTLFormula) : Ï• â‰¡ Ï• := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]

theorem equivalent_ltl_symm (Ï• Ïˆ : LTLFormula) : (Ï• â‰¡ Ïˆ) â†’ (Ïˆ â‰¡ Ï•) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  intro h
  rw [h]

theorem equivalent_ltl_trans (Ï• Ïˆ Ï‡ : LTLFormula) : (Ï• â‰¡ Ïˆ) â†’ (Ïˆ â‰¡ Ï‡) â†’ (Ï• â‰¡ Ï‡) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  intro hâ‚ hâ‚‚
  rw [hâ‚]
  exact hâ‚‚

/-!
It would also be useful to show that equivalence is preserved by certain operations.
-/
theorem equivalent_ltl_preserves_negation (Ï• Ïˆ : LTLFormula) : (Ï• â‰¡ Ïˆ) â†” ((Â¬ Ï•) â‰¡ (Â¬ Ïˆ)) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  constructor
  Â· intro h
    funext Ïƒ
    have hâ‚ : Worlds Ï• Ïƒ = Worlds Ïˆ Ïƒ := by rw [h]
    simp [Worlds] at hâ‚
    simp [Worlds]
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    simp [Not.not]
    simp [Satisfaction.Satisfies] at hâ‚
    rw [hâ‚]
  Â· intro h
    funext Ïƒ
    have hâ‚ : Worlds (Â¬ Ï•) Ïƒ = Worlds (Â¬ Ïˆ) Ïƒ := by
      simp [Worlds]
      rw [â† Worlds, â† Worlds]
      rw [h]
    simp [Worlds] at hâ‚
    simp [Worlds]
    simp [Satisfaction.Satisfies]
    simp [Satisfaction.Satisfies] at hâ‚
    rw [world_satisfies_ltl, world_satisfies_ltl] at hâ‚
    simp [Not.not] at hâ‚
    constructor
    Â· intro h'
      contrapose h'
      rw [hâ‚]
      assumption
    Â· intro h'
      contrapose h'
      rw [â† hâ‚]
      assumption

theorem equivalent_ltl_preserves_always (Ï• Ïˆ : LTLFormula) : (Ï• â‰¡ Ïˆ) â†’ ((â–¡ Ï•) â‰¡ (â–¡ Ïˆ)) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  intro h
  funext Ïƒ
  unfold Worlds
  rw [world_satisfies_always, world_satisfies_always]
  simp
  constructor
  Â· intro h'
    intro i
    specialize h' i
    have hâ‚ : Worlds Ï• (Ïƒ[iâ€¦]) = Worlds Ïˆ (Ïƒ[iâ€¦]) := by rw [h]
    unfold Worlds at hâ‚
    rw [â† hâ‚]
    assumption
  Â· intro h'
    intro i
    specialize h' i
    have hâ‚ : Worlds Ï• (Ïƒ[iâ€¦]) = Worlds Ïˆ (Ïƒ[iâ€¦]) := by rw [h]
    unfold Worlds at hâ‚
    rw [hâ‚]
    assumption

/-!
Now we prove some equivalence rules for LTL formulae.
-/

theorem ltl_double_negation (Ï• : LTLFormula) : (Â¬ (Â¬ Ï•)) â‰¡ Ï• := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  constructor
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl, world_satisfies_ltl] at h
    simp [Not.not] at h
    simp [Satisfaction.Satisfies]
    assumption
  Â· intro h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    simp [Not.not]
    assumption

theorem ltl_duality_next (Ï• : LTLFormula) : ((Â¬ (â—¯ Ï•)) â‰¡ (â—¯ (Â¬ Ï•))) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  constructor

  -- left to right
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl, world_satisfies_ltl] at h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    assumption

  -- right to left
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl, world_satisfies_ltl] at h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl, world_satisfies_ltl]
    assumption

theorem ltl_duality_eventually (Ï• : LTLFormula) : ((Â¬ (â™¢ Ï•)) â‰¡ (â–¡ (Â¬ Ï•))) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  constructor

  -- left to right
  Â· intro h
    simp [Satisfaction.Satisfies] at h
    rw [world_satisfies_ltl] at h
    have hâ‚ : Â¬ (Ïƒ âŠ¨ (â™¢ Ï•)) := by
      simp [Satisfaction.Satisfies]
      assumption
    rw [world_satisfies_eventually] at hâ‚
    simp [Not.not] at hâ‚
    rw [world_satisfies_always]
    intro i
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl]
    specialize hâ‚ i
    simp [Satisfaction.Satisfies] at hâ‚
    apply hâ‚

  -- right to left
  Â· intro h
    simp [Satisfaction.Satisfies]
    rw [world_satisfies_ltl]
    have hâ‚ : Â¬ (Ïƒ âŠ¨ (â™¢ Ï•)) := by
      rw [world_satisfies_eventually]
      simp [Not.not]
      intro i
      simp [Satisfaction.Satisfies]
      rw [world_satisfies_always] at h
      specialize h i
      simp [Satisfaction.Satisfies] at h
      rw [world_satisfies_ltl] at h
      apply h
    simp [Satisfaction.Satisfies] at hâ‚
    apply hâ‚

theorem ltl_duality_always (Ï• : LTLFormula) : ((Â¬ (â–¡ Ï•)) â‰¡ (â™¢ (Â¬ Ï•))) := by
  have hâ‚€ : (Â¬ (â™¢ (Â¬ Ï•))) â‰¡ (â–¡ (Â¬ (Â¬ Ï•))) := ltl_duality_eventually (Â¬ Ï•)
  have hâ‚ : (Â¬ (Â¬ Ï•)) â‰¡ Ï• := ltl_double_negation Ï•
  have hâ‚‚ : (â–¡ (Â¬ (Â¬ Ï•))) â‰¡ (â–¡ Ï•) := equivalent_ltl_preserves_always _ _ hâ‚
  have hâ‚ƒ : (Â¬ â™¢ (Â¬ Ï•)) â‰¡ (â–¡ Ï•) := equivalent_ltl_trans _ _ _ hâ‚€ hâ‚‚
  have hâ‚„ : (Â¬ (Â¬ â™¢ (Â¬ Ï•))) â‰¡ (Â¬ (â–¡ Ï•)) := by
    rw [â† equivalent_ltl_preserves_negation]
    assumption
  have hâ‚… : (Â¬ (Â¬ â™¢ (Â¬ Ï•))) â‰¡ (â™¢ (Â¬ Ï•)) := ltl_double_negation _
  have hâ‚† : (Â¬ (â–¡ Ï•)) â‰¡ (â™¢ (Â¬ Ï•)) := equivalent_ltl_trans _ _ _ hâ‚„ hâ‚…
  assumption

theorem ltl_idempotence_eventually (Ï• : LTLFormula) : (â™¢ (â™¢ Ï•)) â‰¡ (â™¢ Ï•) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  rw [world_satisfies_eventually, world_satisfies_eventually]
  constructor
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    rw [world_satisfies_eventually] at hi
    obtain âŸ¨j, hjâŸ© := hi
    use i + j
    rw [suffix_composition] at hj
    assumption
  Â· intro h
    obtain âŸ¨i, hiâŸ© := h
    use 0
    rw [world_satisfies_eventually]
    use i
    rw [suffix_composition]
    ring_nf
    assumption

theorem ltl_idempotence_always (Ï• : LTLFormula) : (â–¡ (â–¡ Ï•)) â‰¡ (â–¡ Ï•) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  rw [world_satisfies_always, world_satisfies_always]
  constructor
  Â· intro h
    intro i
    specialize h i
    rw [world_satisfies_always] at h
    specialize h 0
    rw [suffix_composition] at h
    assumption
  Â· intro h
    intro i
    rw [world_satisfies_always]
    intro j
    rw [suffix_composition]
    specialize h (i + j)
    assumption

theorem ltl_idempotence_until_left (Ï• Ïˆ : LTLFormula) : ((Ï• ð“¤ Ï•) ð“¤ Ïˆ) â‰¡ (Ï• ð“¤ Ïˆ) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl, world_satisfies_ltl]
  constructor
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    obtain âŸ¨hj', hj''âŸ© := hj
    use j
    constructor
    Â· assumption
    Â· intro k
      intro hk
      have hk' := hj'' k hk
      rw [world_satisfies_ltl] at hk'
      obtain âŸ¨k', hk''âŸ© := hk'
      obtain âŸ¨hkl, hkrâŸ© := hk''
      rw [suffix_composition] at hkl
      specialize hkr 0
      cases c: k' with
      | zero =>
        rw [c] at hkl
        rw [Nat.add_zero] at hkl
        assumption
      | succ n =>
        have p : 0 < k' := by
          rw [c]
          apply Nat.zero_lt_succ
        specialize hkr p
        rw [suffix_composition] at hkr
        rw [Nat.add_zero] at hkr
        assumption
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    obtain âŸ¨hl, hrâŸ© := hj
    constructor
    Â· assumption
    Â· intro k
      intro hk
      rw [world_satisfies_ltl]
      use 0
      rw [suffix_composition, Nat.add_zero]
      specialize hr k hk
      constructor
      Â· assumption
      Â· intro k'
        intro hk'
        simp at hk'

theorem ltl_idempotence_until_right (Ï• Ïˆ : LTLFormula) : (Ï• ð“¤ (Ïˆ ð“¤ Ïˆ)) â‰¡ (Ï• ð“¤ Ïˆ) := by
  simp [Equivalent.Equiv]
  unfold equivalent_ltl
  funext Ïƒ
  simp [Worlds]
  simp [Satisfaction.Satisfies]
  rw [world_satisfies_ltl, world_satisfies_ltl]
  constructor
  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    obtain âŸ¨hl, hrâŸ© := hj
    rw [world_satisfies_ltl] at hl
    obtain âŸ¨j', hj'âŸ© := hl
    obtain âŸ¨hjl, hjrâŸ© := hj'
    rw [suffix_composition] at hjl
    specialize hjr 0
    cases c: j' with
    | zero =>
      rw [c] at hjl
      rw [Nat.add_zero] at hjl
      constructor
      Â· assumption
      Â· assumption
    | succ n =>
      have p : 0 < j' := by
        rw [c]
        apply Nat.zero_lt_succ
      specialize hjr p
      rw [suffix_composition] at hjr
      rw [Nat.add_zero] at hjr
      constructor
      Â· assumption
      Â· assumption

  Â· intro h
    obtain âŸ¨j, hjâŸ© := h
    use j
    obtain âŸ¨hl, hrâŸ© := hj
    constructor
    Â· rw [world_satisfies_ltl]
      use 0
      rw [suffix_composition, Nat.add_zero]
      constructor
      Â· assumption
      Â· intro k
        intro hk
        simp at hk
    Â· assumption

end section
